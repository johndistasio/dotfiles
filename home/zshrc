# Uncomment for startup performance analysis (1/2)
#zmodload zsh/zprof

# From: https://unix.stackexchange.com/questions/62579/is-there-a-way-to-add-a-directory-to-my-path-in-zsh-only-if-its-not-already-pre
# In zsh, $PATH is tied to the "path" array, and -U forces that to have unique values.
# Add elements to the path with "path+=(~/foo)".
typeset -U path PATH

local darwin=false

case "$(uname -s)" in
Darwin*)
  darwin=true;;
esac

# TODO Determine if there is still a use for this.
# Load ANSI color codes into the shell.
# Use like "%{$fg[green]%}", reset with "%{$reset_color%}".
#autoload -Uz colors && colors

# Array of 256-color compatible color escapes.
typeset -A clr
clr[reset]='%F{F}'
clr[red]='%F{9}'
clr[green]='%F{10}'
clr[yellow]='%F{11}'
clr[blue]='%F{12}'
clr[purple]='%F{13}'
clr[cyan]='%F{13}'

# source asdf completions
if [[ -d "$HOME/.asdf/completions" ]]; then 
  fpath=("$HOME/.asdf/completions" $fpath)
fi

# Initialize the completion system.
autoload -Uz compinit && compinit

# Make all completion functions use their verbose forms.
zstyle ':completion:*' verbose true

# List directories first in file completion output.
zstyle ':completion:*' list-dirs-first true

# Always enable menu-style completion even if there are few matches.
zstyle ':completion:*' menu select

# Initialize the version control awareness system.
autoload -Uz vcs_info

# Enable the git backend for vcs_info.
zstyle ':vcs_info:*' enable git

# Detect changes in the local repository so %c and %u will work.
zstyle ':vcs_info:git:*' check-for-changes true

# Formatting for %c (staged changes) format string.
zstyle ':vcs_info:git*' stagedstr "${clr[green]}+${clr[reset]}"

# Formatting for %u (unstaged changes) format string.
zstyle ':vcs_info:git*' unstagedstr "${clr[red]}+${clr[reset]}"

# Normal prompt format string.
vcs_info_git_formats=":${clr[purple]}%b${clr[reset]}%u%c"
zstyle ':vcs_info:git*' formats "${vcs_info_git_formats}"

# Format prompt string used during an "action" like a rebase or merge.
zstyle ':vcs_info:git*' actionformats "${vcs_info_git_formats}:${clr[cyan]}%a${clr[reset]}"

# Change directory by typing directory name if it's not a command.
setopt auto_cd

# Make cd push the old directory onto the directory stack.
setopt auto_pushd

# Add commands to history as they are entered, don't wait for shell to exit.
setopt inc_append_history

# Allow parameter expansion, command substitution, and arithmetic expansion to
# happen in prompts.
setopt prompt_subst

# Executed before each prompt.
precmd() {
  vcs_info
}

# https://zsh.sourceforge.io/Doc/Release/Prompt-Expansion.html
PROMPT='${clr[blue]}%1~${clr[reset]}${vcs_info_msg_0_}%# '

alias cds="$HOME/src"
alias ll='ls -lh --color=auto'
alias ls='ls -h --color=auto'

if $darwin; then
  alias ll='ls -lhG'
  alias ls='ls -hG'

  if [ -f /opt/homebrew/bin/brew ]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
  fi
fi

# Have a lot of fun!
alias ..='cd ..'

# Tidy up and truncate output filenames to avoid hitting filename size limitations.
alias ydl='youtube-dl -o "%(title).150s-%(id)s.%(ext)s" --restrict-filenames'

# Ensure XDG binpath is in PATH.
path+=("$HOME/.local/bin")

# kubernetes
if type kubectl &> /dev/null; then
  path+=("${KREW_ROOT:-$HOME/.krew}/bin")
  alias k="kubectl"
  source <(kubectl completion zsh)
fi

# python
venv() {
  local v="$1"

  mkdir -p ~/.venv

  if [[ ! -n "$v" ]]; then
    v="$(ls ~/.venv/ | fzf)"
  fi

  if [[ -n "$v" ]]; then
    source "$HOME/.venv/$v/bin/activate"
  fi
}

# go
export GOPATH="$HOME/go"
path+=("$GOPATH/bin")
path+=("$HOME/.local/go/bin")

# rust
[[ -f ~/.cargo/env ]] && source ~/.cargo/env

# erlang
# https://adoptingerlang.org/docs/development/setup/
export ERL_AFLAGS="+pc unicode -kernel shell_history enabled"
export KERL_CONFIGURE_OPTIONS="--without-javac"
# rebar3 installed with 'rebar3 local install'
path+=("$HOME/.cache/rebar3/bin")

# ocaml
# opam configuration
[[ ! -r $HOME/.opam/opam-init/init.zsh ]] || source $HOME/.opam/opam-init/init.zsh  > /dev/null 2> /dev/null

# .net
export DOTNET_CLI_TELEMETRY_OPTOUT=true

# Fix navigation cluster keys within zsh
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line
bindkey "^[[2~" quoted-insert
bindkey "^[[3~" delete-char
bindkey "^[[5~" beginning-of-history
bindkey "^[[6~" end-of-history

if [[ -d "$HOME/.antidote" ]] then
  source "$HOME/.antidote/antidote.zsh"
  antidote load "$HOME/.zsh_plugins.txt"
fi

# asdf
[[ -d "$HOME/.asdf" ]] && source "$HOME/.asdf/asdf.sh"

# Stick any site-local configuration in here (i.e. employer-specific config).
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# Uncomment for startup performance analysis (2/2)
#zprof

